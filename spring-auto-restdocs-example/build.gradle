buildscript {
    ext {
        springBootVersion = '1.5.3.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.asciidoctor.convert'

group = 'capital.scalable'
version = '1.0.7'

description = """Spring Auto REST Docs Example Project"""

sourceCompatibility = 1.7
targetCompatibility = 1.7

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

configurations {
    jsondoclet
}

ext {
    snippetsDir = file("$buildDir/generated-snippets")
    javadocJsonDir = file("$buildDir/generated-javadoc-json")
}

repositories {
     mavenLocal()
     mavenCentral()
}
dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    compile group: 'org.springframework.security.oauth', name: 'spring-security-oauth2'
    compile group: 'org.springframework.data', name: 'spring-data-commons'
    compile group: 'javax.el', name: 'javax.el-api'
    compile group: 'capital.scalable', name: 'spring-auto-restdocs-core', version: version
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
        exclude(module: 'commons-logging')
    }
    testCompile group: 'com.jayway.jsonpath', name: 'json-path-assert'
    testCompile group: 'org.springframework.restdocs', name: 'spring-restdocs-core'
    testCompile group: 'org.springframework.restdocs', name: 'spring-restdocs-mockmvc'
    compileOnly group: 'org.projectlombok', name: 'lombok'
    jsondoclet group: 'capital.scalable', name: 'spring-auto-restdocs-json-doclet', version: version
}

task jsonDoclet(type: Javadoc, dependsOn: compileJava) {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
    destinationDir = javadocJsonDir
    options.docletpath = configurations.jsondoclet.files.asType(List)
    options.doclet = 'capital.scalable.restdocs.jsondoclet.ExtractDocumentationAsJsonDoclet'
    options.memberLevel = JavadocMemberLevel.PACKAGE
}

test {
    systemProperty 'org.springframework.restdocs.outputDir', snippetsDir
    systemProperty 'org.springframework.restdocs.javadocJsonDir', javadocJsonDir

    dependsOn jsonDoclet
}

asciidoctor {
    sourceDir = file('src/main/asciidoc')
    outputDir = file("$buildDir/generated-docs")
    options backend: 'html', doctype: 'book'
    attributes 'source-highlighter': 'highlightjs', 'snippets': snippetsDir

    dependsOn test
}

asciidoctor.doLast {
    copy {
        from file("$buildDir/generated-docs/html5")
        into file("$sourceSets.main.output.classesDir/public")
        include 'index.html'
    }
}

jar {
    dependsOn asciidoctor
}
