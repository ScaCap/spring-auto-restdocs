buildscript {
    ext {
        springBootVersion = "2.0.0.M7"
    }
    repositories {
        maven { url 'https://repo.spring.io/milestone' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.3"
    }
}

apply plugin: "java"
apply plugin: "org.springframework.boot"
apply plugin: "org.asciidoctor.convert"

group = "capital.scalable"
version = "2.0.0-SNAPSHOT"

description = """Spring Auto REST Docs Example Project"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

configurations {
    jsondoclet
}

ext {
    snippetsDir = file("$buildDir/generated-snippets")
    javadocJsonDir = file("$buildDir/generated-javadoc-json")
}

repositories {
     mavenLocal()
     mavenCentral()
}
dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.security.oauth:spring-security-oauth2:2.2.1.RELEASE"
    compile "org.springframework.data:spring-data-commons"
    compile "capital.scalable:spring-auto-restdocs-core:$version"
    testCompile("org.springframework.boot:spring-boot-starter-test") {
        exclude(module: "commons-logging")
    }
    testCompile "com.jayway.jsonpath:json-path-assert"
    testCompile "org.springframework.restdocs:spring-restdocs-core:2.0.0.RELEASE"
    testCompile "org.springframework.restdocs:spring-restdocs-mockmvc:2.0.0.RELEASE"
    compileOnly "org.projectlombok:lombok"
    jsondoclet "capital.scalable:spring-auto-restdocs-json-doclet:$version"
}

task jsonDoclet(type: Javadoc, dependsOn: compileJava) {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
    destinationDir = javadocJsonDir
    options.docletpath = configurations.jsondoclet.files.asType(List)
    options.doclet = "capital.scalable.restdocs.jsondoclet.ExtractDocumentationAsJsonDoclet"
    options.memberLevel = JavadocMemberLevel.PACKAGE
}

test {
    systemProperty "org.springframework.restdocs.outputDir", snippetsDir
    systemProperty "org.springframework.restdocs.javadocJsonDir", javadocJsonDir

    dependsOn jsonDoclet
}

asciidoctor {
    sourceDir = file("src/main/asciidoc")
    outputDir = file("$buildDir/generated-docs")
    options backend: "html", doctype: "book"
    attributes "source-highlighter": "highlightjs", "snippets": snippetsDir

    dependsOn test
}

asciidoctor.doLast {
    copy {
        from file("$buildDir/generated-docs/html5")
        into file("$sourceSets.main.output.classesDir/public")
        include "index.html"
    }
}

jar {
    dependsOn asciidoctor
}
