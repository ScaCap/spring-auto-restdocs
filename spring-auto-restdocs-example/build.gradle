apply plugin: 'java'
apply plugin: 'maven'

group = 'capital.scalable'
version = '1.0.5-SNAPSHOT'

description = """Spring Auto REST Docs Example Project"""

sourceCompatibility = 1.7
targetCompatibility = 1.7

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

configurations {
    jsondoclet
}

ext {
    snippetsDir = file("$buildDir/generated-snippets")
    javadocJsonDir = file("$buildDir/generated-javadoc-json")
}

repositories {
     mavenLocal()
     mavenCentral()
}
dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version:'1.4.0.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version:'1.4.0.RELEASE'
    compile group: 'org.springframework.security.oauth', name: 'spring-security-oauth2', version:'2.0.10.RELEASE'
    compile group: 'capital.scalable', name: 'spring-auto-restdocs-core', version:'1.0.5-SNAPSHOT'
    compile group: 'org.springframework.data', name: 'spring-data-commons', version:'1.12.2.RELEASE'
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version:'1.4.0.RELEASE') {
        exclude(module: 'commons-logging')
    }
    testCompile group: 'com.jayway.jsonpath', name: 'json-path-assert', version:'2.2.0'
    testCompile group: 'org.springframework.restdocs', name: 'spring-restdocs-core', version:'1.1.1.RELEASE'
    testCompile group: 'org.springframework.restdocs', name: 'spring-restdocs-mockmvc', version:'1.1.1.RELEASE'
    compileOnly group: 'org.projectlombok', name: 'lombok', version:'1.16.10'
    jsondoclet group: 'capital.scalable', name: 'spring-auto-restdocs-json-doclet', version:'1.0.5-SNAPSHOT'
}

task jsonDoclet(type: Javadoc, dependsOn: compileJava) {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
    destinationDir = javadocJsonDir
    options.docletpath = configurations.jsondoclet.files.asType(List)
    options.doclet = 'capital.scalable.restdocs.jsondoclet.ExtractDocumentationAsJsonDoclet'
}

test {
    systemProperty 'org.springframework.restdocs.outputDir', snippetsDir
    systemProperty 'org.springframework.restdocs.javadocJsonDir', javadocJsonDir

    dependsOn jsonDoclet
}
